dnl configure.ac
dnl
dnl xfce4-notifyd - server-side implementation of the fd.o notifications spec
dnl
dnl Copyright (c) 2008 Brian Tarricone <brian@tarricone.org>
dnl

dnl version info
m4_define([xfce4_notifyd_version_major], [0])
m4_define([xfce4_notifyd_version_minor], [1])
m4_define([xfce4_notifyd_version_micro], [0])
m4_define([xfce4_notifyd_version_build], [@REVISION@])
m4_define([xfce4_notifyd_version_tag], [git])
m4_define([xfce4_notifyd_version], [xfce4_notifyd_version_major().xfce4_notifyd_version_minor().xfce4_notifyd_version_micro()ifelse(xfce4_notifyd_version_tag(), [git], [xfce4_notifyd_version_tag()-xfce4_notifyd_version_build()], [xfce4_notifyd_version_tag()])])

m4_define([notifications_spec_version], [0.9])

dnl init autoconf
AC_INIT([xfce4-notifyd], [xfce4_notifyd_version],
        [http://bugzilla.xfce.org/enter_bug.cgi?product=Xfce4-notifyd],
        [xfce4-notifyd])
AC_PREREQ([2.59c])
AC_CONFIG_SRCDIR([xfce4-notifyd/main.c])
AC_CONFIG_HEADERS([config.h])

dnl init automake
AM_INIT_AUTOMAKE([1.10 dist-bzip2 subdir-objects])
AM_MAINTAINER_MODE

dnl check for UNIX variants
AC_USE_SYSTEM_EXTENSIONS

dnl check for basic programs
AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_INSTALL
IT_PROG_INTLTOOL([0.31], [no-xml])
AM_GNU_GETTEXT_VERSION([0.11])
AM_GLIB_GNU_GETTEXT

dnl check for standard header files
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h fcntl.h  grp.h signal.h stdlib.h string.h \
                  sys/stat.h sys/time.h sys/types.h sys/wait.h \
                  unistd.h])

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

dnl required
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.5.2svn-r26504])
XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [0.1.0svn-r26876])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.10.0])
XDT_CHECK_PACKAGE([DBUS], [dbus-1], [1.0.0])
XDT_CHECK_PACKAGE([DBUS_GLIB], [dbus-glib-1], [0.72])
XDT_CHECK_PACKAGE([LIBGLADE], [libglade-2.0], [2.6.0])

XDT_CHECK_OPTIONAL_PACKAGE([LIBSEXY], [libsexy], [0.1.6], [libsexy],
                           [libsexy support (for clickable URLs) (default=yes)],
                           [yes])

AS_IF([test "x$USE_MAINTAINER_MODE" = "xyes"],
[
  AC_PATH_PROG([EXO_CSOURCE], [exo-csource])
  AS_IF([test -z "$EXO_CSOURCE"],
  [
    echo '*** The program "exo-csource" is required to build when --enable-maintainer-mode'
    echo '*** is specified.'
    exit 1
  ])
])

dnl legacy support
AC_ARG_ENABLE([old-notification-closed-signature],
              [AS_HELP_STRING([--enable-old-notification-closed-signature],
                              [Use the old NotificationClosed signal signature that leaves out the 'reason' parameter.  You want to enable this if your system has libnotify 0.4.4 or earlier.])],
              [use_old_nc_sig=$enableval], [use_old_nc_sig=no])
AS_IF([test "x$use_old_nc_sig" = "xyes"],
      [AC_DEFINE([USE_OLD_NOTIFICATION_CHANGED_SIGNATURE], [1],
                 [Set if we should use the old NotificationClosed signal signature])])

dnl legacy support
AC_ARG_ENABLE([old-get-server-information-signature],
              [AS_HELP_STRING([--enable-old-get-server-information-signature],
                              [Use the old GetServerInformation method signature that leaves out the 'spec_version' parameter. (default=no)])],
              [use_old_gsi_sig=$enableval], [use_old_gsi_sig=no])
AS_IF([test "x$use_old_gsi_sig" = "xyes"],
      [AC_DEFINE([USE_OLD_GET_SERVER_INFORMATION_SIGNATURE], [1],
                 [Set if we should use the old GetServerInformation method signature])])
AM_CONDITIONAL([USE_OLD_GET_SERVER_INFORMATION_SIGNATURE],
               [test "x$use_old_gsi_sig" = "xyes"])

dnl create #define for spec version
AC_DEFINE_UNQUOTED([NOTIFICATIONS_SPEC_VERSION], ["notifications_spec_version"],
                   [Set to the version of the freedesktop.org notifications spec we support])

dnl check for debugging support
XDT_FEATURE_DEBUG

AC_CONFIG_FILES(
[
    Makefile
    po/Makefile.in
])
AC_OUTPUT
